SPL       ::= ( VarDecl | FunDecl )*
VarDecl   ::= ( 'var' | Type ) 'id'  '=' Exp ';'
FunDecl   ::= 'id' '(' [ FArgs ] ')' [ '::' FunType ] '{' VarDecl* Stmt+ '}'
RetType   ::= Type
            | 'Void'
FunType   ::= Type* '->' RetType
Type      ::= BasicType
            | ( '(' Type ',' Type ')' )
            | ( '[' Type ']' )
            | 'id'
BasicType ::= 'Int'
            | 'Bool'
            | 'Char'
FArgs     ::= 'id' [ ',' FArgs ]
Stmt      ::= IfElse
            | While
            | StmtAss
            | ( FunCall ';' )
            | Return
StmtAss   ::= 'id' Field '=' Exp ';'
IfElse    ::= 'if' '(' Exp ')' '{' Stmt* '}' [ 'else' '{' Stmt* '}' ]
While     ::= 'while' '(' Exp ')' '{' Stmt* '}'
Return    ::= 'return' [ Exp ] ';'
Exp       ::= Leq [ Eq' ]
Eq'       ::= ( '==' | '!=' ) Leq [ Eq' ]
Leq       ::= Sum [ Leq' ]
Leq'      ::= ( '<' | '>' | '<=' | '>=' ) Sum [ Leq' ]
Sum       ::= Fact [ Sum' ]
Sum'      ::= ( '+' | '-' | '||' ) Fact [ Sum' ]
Fact      ::= Colon [ Fact' ]
Fact'     ::= ( '*' | '/' | '%' | '&&' ) Colon [ Fact' ]
Colon     ::= Unary [ ':' Colon ]
Unary     ::= ( ( '!' | '-' ) Unary ) | Basic
Basic     ::= ( '(' Exp [ ',' Exp ] ')' ) | 'int' | 'char' | 'False' | 'True' | FunCall | ( '[' ']' ) | ( 'id' Field )
Field     ::= ( '.hd' | '.tl' | '.fst' | '.snd' )*
FunCall   ::= 'id' '(' [ ActArgs ] ')'
ActArgs   ::= Exp [ ',' ActArgs ]